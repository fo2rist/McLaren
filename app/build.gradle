apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply from: '../versions.gradle'
apply from: '../jacoco.gradle'

buildscript { //for crashlytics
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.28.0'
    }
}

android {
    signingConfigs {
        test_sign_config {
            keyAlias 'testing'
            keyPassword 'testing'
            storeFile rootProject.file('testing_dummy_key')
            storePassword 'testing'
        }
    }

    compileSdkVersion project.COMPILE_SDK_VERSION
    buildToolsVersion project.BUILD_TOOLS_VERSION
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def properties = readProperties("env/mclaren.properties")

    defaultConfig {
        applicationId "com.github.fo2rist.mclaren"
        targetSdkVersion project.TARGET_SDK_VERSION
        minSdkVersion project.MIN_SDK_VERSION
        versionCode project.APP_VERSION_CODE
        versionName project.APP_VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [crashlyticsKey: readProperty(properties, 'CRASHLYTICS_KEY')]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.test_sign_config
        }
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            testCoverageEnabled true
        }
    }

    lintOptions {
        warningsAsErrors false
        abortOnError true
        htmlReport true
        baseline file("lint-baseline.xml")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    applicationVariants.all { variant ->
        setConfigProperty(variant, properties, 'MCLAREN_FEED_URL')
        setConfigProperty(variant, properties, 'MCLAREN_RACE_INFO_URL')
        setConfigProperty(variant, properties, 'MCLAREN_RACE_LIFE_DATA_URL')
        setConfigProperty(variant, properties, 'MCLAREN_CDN_AUTH')
        setConfigProperty(variant, properties, 'MCLAREN_TAB_API_AUTH')
        setConfigProperty(variant, properties, 'STORYSTREAM_FEED_URL')
        setConfigProperty(variant, properties, 'STORYSTREAM_TOKEN')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$project.SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$project.SUPPORT_VERSION"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:design:$project.SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$project.SUPPORT_VERSION"
    implementation "com.android.support:support-v4:$project.SUPPORT_VERSION"
    // firebase cloud config
    implementation 'com.google.firebase:firebase-config:16.3.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"

    // zoom image view
    implementation 'com.github.chrisbanes:PhotoView:2.0.0' //2.2 conflicts with old support lib on manifest merge
    // auto link for text view embedded links
    implementation 'com.github.armcha:AutoLinkTextView:0.3.0'
    // dot indicator view
    implementation 'me.relex:circleindicator:1.2.2'

    // networking
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.picasso:picasso:2.5.2' // 2.71828 uses support lib 27.1.0 incompatible with 27.1.1
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // event bus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // joda time
    implementation 'joda-time:joda-time:2.10'

    // logger
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // crashlytics by fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true;
    }

    // dependency injection
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'
    kapt 'com.google.dagger:dagger-android-processor:2.22.1'

    // test libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation 'org.robolectric:shadows-supportv4:4.2'

    // instrumented test libraries
    androidTestImplementation "com.android.support:support-annotations:$project.SUPPORT_VERSION"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2', {
        exclude group: 'com.android.support'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.agoda.kakao:kakao:1.4.0'
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
}

// Must be the last of all statements in this file
apply plugin: 'com.google.gms.google-services'

/**
 * Load bunc of properties from file.
 * @return {@link Properties}
 */
def readProperties(configFileName) {
    def properties = new Properties()
    try {
        properties.load(new FileInputStream(rootProject.file(configFileName)))
        println "Load properties from configuration file"
    } catch (IOException ignore) {
        println "Properties file ${configFileName} not found. Will use ENV variables."
    }
    return properties
}

/**
 * Set build config variable by name.
 * Check properties file first, then ENV, set "null" if not found.
 */
static def setConfigProperty(variant, Properties properties, String propertyName) {
    def propertyValue = readProperty(properties, propertyName)
    variant.buildConfigField "String", propertyName, wrapString(propertyValue)
    if (propertyValue == null)
        System.err.println "Property ${propertyName} not found and set to null for variant ${variant.name}"
}

private static String readProperty(final Properties properties, String propertyName) {
    return properties.getProperty(propertyName) ?: System.getenv(propertyName)
}

/** Wrap string var to be used as string literal in java code. */
static def wrapString(stringVar) {
    return "\"${stringVar}\""
}
